plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

ext {
    junitParamsVersion = '5.4.0'
    junitMockitoVersion = '2.24.0'
    mockitoKotlinVersion = '2.2.0'
    assertjVersion = '3.12.2'
    bouncycastleVersion = '1.61'
    web3Version = '4.5.9'
    cucumberVersion = '4.7.2'
    apacheCxfVersion = '3.3.5'
    jakartaVersion = '2.1.6'
    jacksonJsonProviderVersion = '2.10.2.1'
    jacksonYamlDataFormatVersion = '2.10.2'
    jacksonModuleKotlin = '2.10.2'
    httpComponentsVersion = '4.5.11'
}

allprojects {
    group 'org.kevem'
    version '0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    repositories {
        mavenCentral()
    }

    test {
        dependsOn 'cleanTest'

        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.test
    getExecutionData().from("**/build/jacoco/*.exec")

    subprojects.each {
        if (!it.path.contains("compat") && !it.path.contains("ethereumtests") ) {
            sourceSets(it.sourceSets.main)
        }
    }

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}