group 'com.gammadex'
version '1.0-SNAPSHOT'

ext {
    junitParamsVersion = '5.4.0'
    junitMockitoVersion = '2.24.0'
    mockitoKotlinVersion = '2.2.0'
    assertjVersion = '3.12.2'
    bouncycastleVersion = '1.61'
    web3Version = '4.5.9'
    cucumberVersion = '4.7.2'
    apacheCxfVersion = '3.3.5'
    jakartaVersion = '2.1.6'
    jacksonJsonProviderVersion = '2.10.2.1'
    jacksonModuleKotlin = '2.10.2'
    httpComponentsVersion= '4.5.11'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco' // TODO re-enable when back online

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    repositories {
        mavenCentral()
    }

    test {
        dependsOn 'cleanTest'

        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.test
    executionData = fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        if (! it.path.contains("compat")) {
            sourceSets(it.sourceSets.main)
        }
    }

    reports {
        xml.enabled = true
        html.enabled = true
        csv.enabled = false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}